#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьСообщенияКанала(РазмерВыборкиСообщений = 100) Экспорт

	Если Не ПроверитьЗаполнение() Тогда 
		Возврат;
	КонецЕсли;
	
	СообщенияДляОтображения = СервисыИнтеграции[СервисИнтеграции][КаналСервисаИнтеграции]
					.ВыбратьСообщения(, РазмерВыборкиСообщений);
			
	СообщенияШины.Очистить();
	Для Каждого СообщениеИнтеграции Из СообщенияДляОтображения Цикл
		
		ДобавитьСтрокуСообщенийШины(СообщениеИнтеграции);
	КонецЦикла;
КонецПроцедуры

Процедура НайтиСообщениеКаналаПоТелу(СтрокаПоиска) Экспорт
	Перем НайденноеСообщение;
	ВсеСообщения = СервисыИнтеграции[СервисИнтеграции][КаналСервисаИнтеграции]
		.ВыбратьСообщения();
		
	СообщенияШины.Очистить();	
	Для Каждого СообщениеПоиска Из ВсеСообщения Цикл
		
		ТелоСтрокой = ТелоСообщенияСтрокой(СообщениеПоиска);
		Если НЕ СтрНайти(ТелоСтрокой, СтрокаПоиска) = 0 тогда
			ДобавитьСтрокуСообщенийШины(СообщениеПоиска);
		КонецЕсли;
		
	КонецЦикла

КонецПроцедуры

// Добавить строку сообщений шины.
// 
// Параметры:
//  СообщениеИнтеграции - СообщениеСервисаИнтеграции - Сообщение интеграции
Процедура ДобавитьСтрокуСообщенийШины(СообщениеИнтеграции)
	Перем ПредставлениеСообщения;
	ИмяПараметраПредставления = ТРег("Presentation"); //Представление
	ИмяПараметраМетод = ТРег("Method"); //Метод
	ИмяПараметраИдентификаторОбъекта = ТРег("ObjectId"); //ИдентификаторОбъекта
	ИмяПараметраВерсия = ТРег("Version"); //Версия
	ИмяПараметраТипСодержимого = ТРег("ContentType"); //ТипСообщенияОбмена
	ПараметрыСообщения = НормализованноеСоответсвие(СообщениеИнтеграции.Параметры);
	
	СтрокаСообщенийШины = СообщенияШины.Добавить();
	СтрокаСообщенийШины.Сообщение =  СообщениеИнтеграции.Идентификатор;
	СтрокаСообщенийШины.Отправитель = СообщениеИнтеграции.КодОтправителя;
	СтрокаСообщенийШины.Получатель = СообщениеИнтеграции.КодПолучателя;
	СтрокаСообщенийШины.РазмерТела = СообщениеИнтеграции.РазмерТела;
	СтрокаСообщенийШины.Запрос = СообщениеИнтеграции.ИдентификаторСообщенияЗапроса;
	СтрокаСообщенийШины.Отправлено = СообщениеИнтеграции.ДатаОтправки;
	СтрокаСообщенийШины.Устареет = СообщениеИнтеграции.ДатаУстаревания;
	СтрокаСообщенийШины.Параметры = ОбщегоНазначения.ЗначениеВСтрокуXML(СообщениеИнтеграции.Параметры);
	
	ПредставлениеСообщения = ПараметрыСообщения.Получить(ИмяПараметраПредставления);
	Если ЗначениеЗаполнено(ПредставлениеСообщения) Тогда
		СтрокаСообщенийШины.Представление = РаскодироватьСтроку(ПредставлениеСообщения, СпособКодированияСтроки.КодировкаURL);
	Иначе
		СтрокаСообщенийШины.Представление = СтандартноеПредставлениеСообщения(СообщениеИнтеграции);
	КонецЕсли;
	
	СтрокаСообщенийШины.Метод = ПараметрыСообщения.Получить(ИмяПараметраМетод);
	СтрокаСообщенийШины.ИдентификаторОбъекта = ПараметрыСообщения.Получить(ИмяПараметраИдентификаторОбъекта);
	СтрокаСообщенийШины.Версия = ПараметрыСообщения.Получить(ИмяПараметраВерсия);
	СтрокаСообщенийШины.ТипСодержимого = ПараметрыСообщения.Получить(ИмяПараметраТипСодержимого);
КонецПроцедуры

Функция НормализованноеСоответсвие(ВходящееСоответсвие)
	
	НормализованноеСоответсвие = Новый Соответствие();
	Для Каждого КлючИЗначение ИЗ ВходящееСоответсвие Цикл
		НормализованноеСоответсвие.Вставить(ТРег(КлючИЗначение.Ключ), КлючИЗначение.Значение);
	КонецЦикла;
	
	Возврат НормализованноеСоответсвие;
КонецФункции
Функция ТелоСообщенияСтрокой(Сообщение)

	Если Сообщение.РазмерТела = 0 Тогда
		РазмерБуфера = 1024;
	Иначе
		РазмерБуфера = Сообщение.РазмерТела;
	КонецЕсли;
	
	Тело  = Новый БуферДвоичныхДанных(0);
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
	Поток = Сообщение.ПолучитьТелоКакПоток();
	
	Пока Истина Цикл
		Прочитано = Поток.Прочитать(Буфер, 0, РазмерБуфера);
		Если Прочитано > 0 Тогда
			Тело = Тело.Соединить(Буфер);  
		КонецЕсли;
		Если Прочитано < РазмерБуфера Тогда
			Прервать; 
		КонецЕсли; 
	КонецЦикла;
	
	ТелоСтрокой = ПолучитьСтрокуИзБуфераДвоичныхДанных(Тело);
	Возврат ТелоСтрокой;

КонецФункции

Функция СтандартноеПредставлениеСообщения(СообщениеИнтеграции)
	Результат = СтрШаблон("Сообщение Ид:%1 от %2 для %3 отправленное %4",
					СообщениеИнтеграции.Идентификатор,
					СообщениеИнтеграции.КодОтправителя,
					СообщениеИнтеграции.КодПолучателя,
					СообщениеИнтеграции.ДатаОтправки);
					
	Возврат Результат;
КонецФункции

#КонецОбласти

#КонецЕсли