#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.СервисИнтеграции = Параметры.СервисИнтеграции;
	Объект.КаналСервисаИнтеграции = Параметры.КаналСервисаИнтеграции;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)


КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отправить(Команда)
	ОтправитьСообщениеНаСервере();
	ОповеститьОЗаписиНового(Неопределено);
	ЭтотОбъект.Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура ОтправитьСообщениеНаСервере()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;

	Интеграция = СервисыИнтеграции[Объект.СервисИнтеграции];
	
	ОтправляемоеСообщение = Интеграция.СоздатьСообщение();
	ОтправляемоеСообщение.КодОтправителя = ЭтотОбъект.КодОтправителя;
	ОтправляемоеСообщение.КодПолучателя = ЭтотОбъект.КодПолучателя;

	Для Каждого Параметр Из ПараметрыСообщения Цикл
		ОтправляемоеСообщение.Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТелоСообщения) Тогда
		ОтправляемыйПоток = ОтправляемоеСообщение.ПолучитьТелоКакПоток();
		ТелоБуфером = ПолучитьБуферДвоичныхДанныхИзСтроки(ТелоСообщения);
		ОтправляемыйПоток.Записать(ТелоБуфером, 0, ТелоБуфером.Размер);
		ОтправляемыйПоток.СброситьБуферы();
		ОтправляемыйПоток.Закрыть();
	КонецЕсли;
	
	Интеграция[Объект.КаналСервисаИнтеграции].ОтправитьСообщение(ОтправляемоеСообщение);	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТелоСообщенияСтрокой(Знач Сообщение)

	Если Сообщение.РазмерТела = 0 Тогда
		РазмерБуфера = 1024;
	Иначе
		РазмерБуфера = Сообщение.РазмерТела;
	КонецЕсли;
	
	Тело  = Новый БуферДвоичныхДанных(0);
	Буфер = Новый БуферДвоичныхДанных(РазмерБуфера);
	Поток = Сообщение.ПолучитьТелоКакПоток();
	
	Пока Истина Цикл
		Прочитано = Поток.Прочитать(Буфер, 0, РазмерБуфера);
		Если Прочитано > 0 Тогда
			Тело = Тело.Соединить(Буфер);  
		КонецЕсли;
		Если Прочитано < РазмерБуфера Тогда
			Прервать; 
		КонецЕсли; 
	КонецЦикла;
	
	ТелоСтрокой = ПолучитьСтрокуИзБуфераДвоичныхДанных(Тело);
	Возврат ТелоСтрокой;

КонецФункции

&НаСервере
Процедура ЗаполнитьКаналы()
	
	Элементы.КаналСервисаИнтеграции.СписокВыбора.Очистить();
	Если НЕ ЗначениеЗаполнено(Объект.СервисИнтеграции) Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ОбъектМетаданных Из Метаданные.СервисыИнтеграции[Объект.СервисИнтеграции].КаналыСервисаИнтеграции Цикл
		Элементы.КаналСервисаИнтеграции.СписокВыбора.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним); 
	КонецЦикла;

КонецПроцедуры

#КонецОбласти